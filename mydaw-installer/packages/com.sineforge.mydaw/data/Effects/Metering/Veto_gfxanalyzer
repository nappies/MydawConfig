// desc:Multi Channel Spectrum Analyzer (Veto_gfxanalyzer)
// provides:
//   [windows] .
//   [darwin] .
// tags: analysis FFT meter spectrum multi channel
// author: Cockos/Veto
// version: 0.2.2
// screenshot:
//   https://i.imgur.com/faNmSdz.gif
// donation:
//   https://www.cockos.com/reaper/purchase.php
//   https://reapack.com/
// changelog:
//   # Tiny internal change (unambiguous effect description)
// about:
//   ## Description
//   Multi-channel mod of Cockos gfxanalyzer (Frequency Spectrum Analyzer Meter included in REAPER) by [Veto](https://forum.cockos.com/member.php?u=25032).
//   ![Image](https://i.imgur.com/USvPDCk.gif)
//   
//   See also [here](https://forum.cockos.com/showthread.php?t=200414).
//
//   ## Features
//   (additional to gfxanalyzer)
//    - Frequency/notes & amplitude readout
//    - Channel on/off switches
//    - Spectrum slope/tilt
//    - Opacity control
//    - Maximum contrast coloring
//    - customize plot colors by setting parameter values (see below)
//
//   ## Controls
//    - **left-click**: show/hide controls/readout
//
//   ## Customizing plot colors
//   By default colors of all stereo channels are hard-coded to have maximum human-perceivable contrast to each other and to the background.
//   Using for instance ReaScript the user can override this by setting values of effect parameters named `color 1-2`, `color 3-4` etc.
//   Values to be sent/received are RGB channels packed into 24bit int (`pc=r+g*256+b*65536`).
//
//   I include a script in my repo which modifies plot colors of the currently focused Veto_gfxanalyzer plugin to colors of tracks which send to stereo channels 1-16 on the receiving track
//   (fx-chain routing and plugin pin connection has to be unmodified).
//  
//   Look for a EEL script named `"Set colors of focused Veto_gfxanalyzer to tracks sending on channels 1-16.eel"`.
//  
//   **Short example (EEL):**
//  
//   This changes the color of stereo channel 15-16 to yellow.
//   The variable`fxID` needs to be set to the index of Veto_gfxanalyzer in the fx-chain of track `trkP`.
//   ```
//   function rpr_TrackFX_SetParamByName(track,fx,paramname,val) local(i,found,curNm) (
//     i=found=0;
//     while(i<TrackFX_GetNumParams(track,fx) && !found ) (
//       TrackFX_GetParamName(track,fx,i,curNm=#);
//       !strcmp(curNm,paramname) ? (
//         TrackFX_SetParam(track,fx,i,val);
//         found=1;
//       );
//       i+=1;
//     );
//     found;
//   );
//  
//   ...
//   rpr_TrackFX_SetParamByName(trkP,fxID,"color 15-16",255+255*256+0*65536);
//   ...
//   ```
//   Copyright (C) 2007 and later, [Cockos Incorporated](http://cockos.com/)
/*
Copyright (C) 2007 and later, Cockos Incorporated
License: LGPL - http://www.gnu.org/licenses/lgpl.html
*/

slider1:6<0,9,1{16,32,64,128,256,512,1024,2048,4096,8192,16384,32768}>-FFT size
slider2:-120<-450,-12,6>-floor
slider3:0<0,1,1{disabled,enabled}>-show phase
slider4:2<0,3,1{rectangular,hamming,blackman-harris,blackman}>-window
slider5:300<0,2500,1>-integration time (ms)
slider6:0.13<0,1,0.01>-opacity
slider7:15<0,255,1>-enable plots
slider8:0<-9,9,0.1>-slope
// // Colors set to most distinguishable in LAB colorspace
// // (based on [0,0,0] [0,0,0.75] [0.6,0.,0.8] and [1,1,0])
slider31:   65535<0,16777215,1>-color 1-2
slider32: 1179903<0,16777215,1>-color 3-4
slider33:15597312<0,16777215,1>-color 5-6
slider34:13283583<0,16777215,1>-color 7-8
slider35:   29237<0,16777215,1>-color 9-10
slider36:16157952<0,16777215,1>-color 11-12
slider37: 4652800<0,16777215,1>-color 13-14
slider38: 2919891<0,16777215,1>-color 15-16

in_pin:1 - left in
in_pin:2 - right in
in_pin:3 - left in
in_pin:4 - right in
in_pin:5 - left in
in_pin:6 - right in
in_pin:7 - left in
in_pin:8 - right in
in_pin:9 - left in
in_pin:10 - right in
in_pin:11 - left in
in_pin:12 - right in
in_pin:13 - left in
in_pin:14 - right in
in_pin:15 - left in
in_pin:16 - right in
options:no_meter

@init

LINUX_COMP=0;  // workaround for bug https://forum.cockos.com/showthread.php?p=1903928#post1903928 on Ubuntu 14.04

function init_bufs()
  instance(recpos,fftworkspace,integrate_buf,startpos)
(
  recpos = startpos;
  fftworkspace = startpos+histsize;
  fftworkspace = ceil(fftworkspace/65536)*65536; // https://forum.cockos.com/showpost.php?p=1922136&postcount=6
  integrate_buf = fftworkspace + max_fft_size*2;
  integrate_buf + max_fft_size;
);
function get_spls(spls)
  instance(recpos,update,startpos,en)
(
  en ? (
    abs(recpos[]=spls) > minvol ? update=1;
    recpos = ((recpos+1) >= startpos+histsize ? startpos : (recpos+1));
  );
);
function check_enab(enab)
  instance(en,len,startpos)
(
  (en=enab) ? len=en
  : len ? (
    memset(startpos,0,histsize);
    len=en;
  );
);
function prgb2rgb(prgb) (
  stack_push((prgb&0xff0000)>>16);
  stack_push((prgb&0x00ff00)>>8);
  stack_push(prgb&0x0000ff);
);
function prgb2rgbfloat(prgb) local(tmpR,tmpG,tmpB) (
  prgb2rgb(prgb);
  tmpR=stack_pop();
  tmpG=stack_pop();
  tmpB=stack_pop();
  stack_push(tmpB/255);
  stack_push(tmpG/255);
  stack_push(tmpR/255);
);
function setColor(sl)
  instance(r,g,b,lColSlider)
(
  sl|=0;
  sl!=lColSlider ? (
    lColSlider=sl;
    prgb2rgbfloat(sl);
    r=stack_pop(); g=stack_pop(); b=stack_pop();
    old_w=0;
  );
);

gfx_ext_retina=1.0;
ext_nodenorm=1;
ext_noinit=1;
max_fft_size=32768;
fftsize=max_fft_size;
gfx_clear=-1;
windowtype=-1;
fftidx=-1;
histsize=max_fft_size + (max_fft_size*0.5 - 1);
show_ctrls=0;
lgfx_ext_retina=-1;
pad1=3; pad2=2; pad3=8;

window=0;
slope_coeffs=window+(max_fft_size*0.5 + 1);
c1.startpos=slope_coeffs+max_fft_size*0.5;
c2.startpos=c1.init_bufs();
c3.startpos=c2.init_bufs();
c4.startpos=c3.init_bufs();
c5.startpos=c4.init_bufs();
c6.startpos=c5.init_bufs();
c7.startpos=c6.init_bufs();
c8.startpos=c7.init_bufs();
c8.init_bufs();

@slider
slider2 != lfloor ? old_w=0;

c1.setColor(slider31);
c2.setColor(slider32);
c3.setColor(slider33);
c4.setColor(slider34);
c5.setColor(slider35);
c6.setColor(slider36);
c7.setColor(slider37);
c8.setColor(slider38);

@block
slider2 != lfloor ? (
  lfloor = slider2;
  minvol=2*exp(log(10)/20*slider2);
);

c1.check_enab(slider7&1);
c2.check_enab(slider7&2);
c3.check_enab(slider7&4);
c4.check_enab(slider7&8);
c5.check_enab(slider7&16);
c6.check_enab(slider7&32);
c7.check_enab(slider7&64);
c8.check_enab(slider7&128);

@sample
c1.get_spls(spl0+spl1);
c2.get_spls(spl2+spl3);
c3.get_spls(spl4+spl5);
c4.get_spls(spl6+spl7);
c5.get_spls(spl8+spl9);
c6.get_spls(spl10+spl11);
c7.get_spls(spl12+spl13);
c8.get_spls(spl14+spl15);


@gfx 640 400

function draw_button_core(xp, yp, str, strr,strg,strb,stra, fillr,fillg,fillb,filla)
  instance(w,h,x,y)
  globals(gfx_x,gfx_y,pad1,pad2)
(
  gfx_measurestr(str, w, h);
  xp -= w+pad1;
  x=xp;
  y=yp;
  gfx_set(fillr,fillg,fillb,filla);
  w+=pad1;
  h+=pad1;
  gfx_rect(x,y,w,h);
  gfx_set(strr,strg,strb,stra);
  gfx_rect(x,y,w,h,0);
  h+=1;
  w+=1;
  gfx_x=xp+pad2; gfx_y=yp+pad2;
  gfx_drawstr(str);
  gfx_x = xp;
);
function draw_button(xp, yp, str, flags)
(
  flags&1 ? // active?
    this.draw_button_core(xp,yp,str, 0,.5,1,1, 0,0,.75,1) :
    this.draw_button_core(xp,yp,str, .3,.3,.3,1, 0,0,0,1);
);
function draw_colorbutton(xp, yp, str, flags)
  instance(r,g,b)
(
  flags&1 ?
    this.draw_button_core(xp,yp,str, r,g,b,1, 0,0,0,1) :
    this.draw_button_core(xp,yp,str, .3,.3,.3,1, 0,0,0,1)
);
function draw_readout(xp, yp, str)
  globals(gfx_x,gfx_y,gfx_w,pad1,pad2)
  local(read_offs,w,h)
(
  read_offs=8;
  gfx_measurestr(str, w, h);
  w+=pad1;
  h+=pad1;
  xp>gfx_w-w-read_offs ? xp-=w+read_offs-1 : xp+=read_offs;
  yp<h+read_offs ? yp+=read_offs : yp-=h+read_offs-1;
  this.draw_button_core(xp+w,yp,str, 0,.5,1,1, 0,0,.75,1)
);
function integr_reset_all() (
  c1.integrate_en=0;
  c2.integrate_en=0;
  c3.integrate_en=0;
  c4.integrate_en=0;
  c5.integrate_en=0;
  c6.integrate_en=0;
  c7.integrate_en=0;
  c8.integrate_en=0;
);
function draw_spectrum()
  instance(r,g,b, update,recpos,fftworkspace,integrate_buf,startpos,
    integrate_now,integrate_lastt,integrate_en,integrate_sc)
  global(window,fftsize,histsize,max_fft_size,slope_coeffs, slider3,slider5,slider6,slider8, buf1,buf2,buf3,
    gfx_x,gfx_y,gfx_h,gfx_r,gfx_g,gfx_b,gfx_a, wsc,sc)
  local(ascale,xscale, lx0,lx,ly,ly2, tx0,tx,ty,ty2, fill_slmin,fill_slast, i,ang)
(
  update ? (
    update=0;

    integrate_sc = 1;
    slider5 > 0 ? (
      integrate_now = time_precise();
      integrate_en ? (
        // fps = 1/(integrate_now - integrate_lastt);
        integrate_sc -= exp(-5 * 1000.0 * (integrate_now - integrate_lastt) / slider5);
      ) : (
        // force overwrite of buffer
        integrate_en = 1;
      );
      integrate_lastt = integrate_now;
    ) : (
      integrate_en = 0;
    );

    buf1=recpos-fftsize;
    buf1<startpos ? buf1+=histsize;
    buf2=window;
    buf3=fftworkspace;
    loop(fftsize*.5 + 1,
      buf3[] = buf1[]*buf2[];
      buf3+=1;

      buf2+=1;
      (buf1+=1) >= startpos+histsize ? buf1 -= histsize;
    );
    buf2-=1;
    loop(fftsize*.5 - 1,
      buf3[] = buf1[]*(buf2-=1)[];
      buf3+=1;
      (buf1+=1) >= startpos+histsize ? buf1 -= histsize;
    );

    fft_real(fftworkspace,fftsize);
    fft_permute(fftworkspace,fftsize/2);
    fftworkspace[1]=0;

    ascale=gfx_h/$pi*0.25;
    xscale=800/(fftsize-4);

    buf3=fftworkspace;
    buf2=integrate_buf+max_fft_size*0.5;
    i=0;
    lx=0;
    slider3 ? loop(fftsize*0.5,
      ang=-atan2(buf3[1],buf3[]);
      buf3+=2;

      integrate_en ? (
        ang = buf2[] += integrate_sc * (ang - buf2[]);
        buf2+=1;
      );


      ty2=ang*ascale + gfx_h*0.5;
      tx = log(1.0+i*xscale)*wsc;

      i ?
      (
        gfx_r=r; gfx_g=g; gfx_b=b; gfx_a=1;
        gfx_x=lx; gfx_y=ly2; gfx_lineto(tx,ty2,1) ;
      );

      lx=tx; ly2=ty2;
      i+=1;
    );

    buf3=fftworkspace;
    buf2=integrate_buf;
    buf1=slope_coeffs;
    lx=0;
    i=0;
    fill_slmin=gfx_h;
    fill_slast=0;
    loop(fftsize*0.5,
      ty = log(max(sqr(buf3[0])+sqr(buf3[1]), 10^(-500/20*2)));
      buf3+=2;

      integrate_en ? (
        ty = buf2[] += integrate_sc * (ty - buf2[]);
        buf2+=1;
      );


      slider8 ? (
        ty+=buf1[];
        buf1+=1;
      );

      ty = ty*-0.5*sc + 20;
      tx = log(1.0+i*xscale)*wsc;

      i ?
      (
        gfx_r=r; gfx_g=g; gfx_b=b;

        1/*fill?*/ ? (
          gfx_a=slider6;
          tx0=tx|0;
          lx0=lx|0;
          tx0>lx0? (
            ly < gfx_h || ty < gfx_h ? gfx_triangle(lx0,max(gfx_h,ly),lx0,ly,tx0-1,ty,tx0-1,max(gfx_h,ty));
          ) : (
            tx0 > fill_slast ? (
             fill_slast < gfx_h ? gfx_line(fill_slast,gfx_h,fill_slast,fill_slmin);
             fill_slmin=gfx_h;
           ) : (
             fill_slmin=min(fill_slmin,ty);
            );
          );
          fill_slast=tx0;
        );
        gfx_a=1;
        gfx_x=lx; gfx_y=ly; gfx_lineto(tx,ty,1) ;
      );

      ty<gfx_h ? update=1;

      lx=tx; ly=ty; ly2=ty2;
      i+=1;
    );
  );
);

function hit_button(xp,yp,cm)
  instance(w,h,x,y)
  globals(cap_mode, cap_last_x, cap_last_y)
(
  xp>=x&&yp>=y&&xp<x+w&&yp<y+h ? (
    cap_last_x = xp;
    cap_last_y = yp;
    cap_mode=cm;
  );
);
function drag_slider(x, y, z, dx)
  globals(mouse_y, cap_last_y, cap_drag)
(
  x = min(max(x + dx * (cap_last_y-mouse_y),y),z);
  cap_last_y=mouse_y;
  cap_drag=1;
  x;
);
function drag_slider_precise(x, y, z, dx)
  globals(mouse_cap)
(
  (mouse_cap & 4) ? dx *= 0.1;
  drag_slider(x, y, z, dx);
);
function cycle_slider(x, y, z, dx)
  globals(last_mouse_cap)
(
  (last_mouse_cap & 16) ? x -= dx : x += dx;
  y > z ? ( dx=y; y=z; z=dx; );
  x > z ? y : x < y ? z : x;
);

function round(in) (
  in=floor(in+0.5*sign(in));
);
function int2str(intIn) local (outStr) (
  strcpy(outStr=#,"");
  sprintf(outStr,"%d",intIn);
  outStr;
);
function freq2note(f) (
  round(12*(log(f/440)/log(2))+69);
);
function note2notename(n,upper) local(oct,nn,keys) (
  upper ? keys="CCDDEFFGGAAH" : keys="ccddeffggaah";
  oct=floor(n/12);oct-=1;
  n%=12;
  strcpy_substr(nn=#,keys,n,1);
  n==1 || n==3 || n==6 || n==8 || n==10 ? strcat(nn,"#");
  strcat(nn,int2str(oct));
  nn;
);



gfx_ext_retina!=lgfx_ext_retina ? (
  lgfx_ext_retina=gfx_ext_retina;
  gfx_ext_retina>1 ? gfx_setfont(1,"Arial",16*gfx_ext_retina,'b') : gfx_setfont(0);
  row2y=gfx_texth+2*pad1;
);

(mouse_cap & 1) ? (
  !(last_mouse_cap & 1) ? (
    show_ctrls ? (
      (cap_mode == 1||cap_mode == 4||cap_mode == 5||cap_mode == 6) && !cap_drag && cap_timer < 12 ? (
        cap_mode == 1 ?
          slider_automate(slider2=-120)
        :cap_mode == 4 ?
          slider_automate(slider5=300)
        :cap_mode == 5 ?
          slider_automate(slider6=0.13)
        :cap_mode == 6 ?
          slider_automate(slider8=0.0);
        cap_mode=0;
      ) : (
        cap_mode = cap_drag = cap_timer = 0;
        phase_button.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider3=!slider3)
        :c1.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=1)
        :c2.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=2)
        :c3.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=4)
        :c4.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=8)
        :c5.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=16)
        :c6.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=32)
        :c7.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=64)
        :c8.hit_button(mouse_x,mouse_y,-1) ?
          slider_automate(slider7~=128)
        : (
          !(floor_button.hit_button(mouse_x,mouse_y,1)||
            window_button.hit_button(mouse_x,mouse_y,2)||
            fft_button.hit_button(mouse_x,mouse_y,3)||
            integrate_button.hit_button(mouse_x,mouse_y,4)||
            opacity_button.hit_button(mouse_x,mouse_y,5)||
            slope_button.hit_button(mouse_x,mouse_y,6)
          ) ? show_ctrls=0;
        );
      );
    ) : show_ctrls=1;
  );
  cap_mode == 1 && cap_last_y != mouse_y ? (
    slider2 = drag_slider_precise(slider2, -450, -12, 0.2);
    slider_automate(slider2);
  );
  cap_mode == 2 && cap_last_y != mouse_y ? (
    slider4 = drag_slider(slider4, 0.0, 3.0, .03);
    slider_automate(slider4);
  );
  cap_mode == 3 && cap_last_y != mouse_y ? (
    slider1 = drag_slider(slider1, 0.0, 11.0, .03);
    slider_automate(slider1);
  );
  cap_mode == 4 && cap_last_y != mouse_y ? (
    slider5 = drag_slider_precise(slider5, 0.0, 2500.0, 5);
    slider_automate(slider5);
  );
  cap_mode == 5 && cap_last_y != mouse_y ? (
    slider6 = drag_slider_precise(slider6, 0.0, 1.0, .005);
    slider_automate(slider6);
  );
  cap_mode == 6 && cap_last_y != mouse_y ? (
    slider8 =  drag_slider_precise(slider8, -9.0, 9.0, .1);
    slider_automate(slider8);
  );
  old_w=0;
) :
(last_mouse_cap & 1) ? (
  !cap_drag ? (
    cap_mode == 2 ? (
      slider4 = cycle_slider(slider4, 0.0, 3.0, 1.0);
      old_w=0;
      slider_automate(slider4);
    );
    cap_mode == 3 ? (
      slider1 = cycle_slider(slider1, 0.0, 11.0, 1.0);
      old_w=0;
      slider_automate(slider1);
    );
  ) : cap_drag=0;
);

cap_mode && cap_timer < 12 ? cap_timer += 1;
last_mouse_cap = mouse_cap;

// on resize
old_w != gfx_w || old_h!=gfx_h ? (

  old_w=gfx_w; old_h=gfx_h;
  gfx_dest=0;
  gfx_setimgdim(gfx_dest,-1,-1);
  gfx_setimgdim(gfx_dest,gfx_w,gfx_h);

  gfx_r=gfx_g=gfx_b=0; gfx_a=1;
  gfx_x=gfx_y=0;
  gfx_rectto(gfx_w,gfx_h);

  sc=(gfx_h-20)*20/(-slider2 * log(10));


  // draw horz grid
  gfx_r=gfx_g=gfx_b=0.6;
  gfx_a=0.5;
  gv=1;
  cnt=100;
  gfx_y=-100;
  while(
    y=20-log(gv)*sc;
    y> gfx_y ? (
      gfx_line(0,y,gfx_w,y,0);
      gfx_x=0;
      gfx_y=y+2;
      slider8==0 ? (
        gfx_drawnumber(log10(gv)*20,0);
        gfx_drawchar($'d');
        gfx_drawchar($'B');
      );
      gfx_y+=gfx_texth;
    );
    gv*=0.5;

    gfx_y<gfx_h && cnt-=1 > 0;
  );


  wsc=gfx_w/log(1+400);

  // draw vert grid
  f=20;
  gfx_x+=4;
  lx=gfx_x;
  while(
    tx = log(1.0+(f/srate*2.0)*400)*wsc;
    dotext = tx > gfx_x && f!=40 && f!=4000 && f != 15000 &&
       (f<400 || f >= 1000 || f == 500) && (f<6000 || f>=10000);
    tx > lx ? ( lx=tx+4; gfx_line(tx,0,tx,gfx_h - (dotext ? 0 : gfx_texth+2),0); );
    dotext ? (
      gfx_x=tx +3;
      gfx_y = gfx_h-gfx_texth;
      f>=1000 ? gfx_printf("%dkHz",f/1000) : gfx_printf("%dHz",f);  // changing f*.001 to f/1000 because of strange issues when inserting by TrackFX_GetByName() (freq grid values are off; no, effect width is not the cause; OS: Ubuntu 14.04)
    );
    f += (f<100?10:f<1000?100:f<10000?1000:5000);
    f < srate*0.5;
  );


  // draw controls
  show_ctrls ? (
    gfx_dest=1;
    gfx_setimgdim(gfx_dest,-1,-1);
    gfx_setimgdim(gfx_dest,gfx_w,row2y+gfx_texth+pad1);

    LINUX_COMP ? (
      gfx_r=gfx_g=gfx_b=0; gfx_a=1;
      gfx_rect(0,0,gfx_w,gfx_h);
    );

    fft_button.draw_button(gfx_w, 0, sprintf(#,"FFT: %d",16<<slider1), 1);
    window_button.draw_button(gfx_x-pad3, 0,
      (slider4|0)==1 ? "hamming" :
      (slider4|0)==2 ? "blackman-harris" :
      (slider4|0)==3 ? "blackman" :
      "rectangular", 1);
    floor_button.draw_button(gfx_x-pad3,0, sprintf(#,"floor: %.1fdB",slider2), 1);
    integrate_button.draw_button(gfx_x-pad3,0, sprintf(#,"integrate: %dms",slider5|0), slider5|0!=0);
    phase_button.draw_button(gfx_x-pad3,0, slider3?"phase on" : "phase off", slider3);

    slope_button.draw_button(gfx_w,row2y, sprintf(#,"Slope: %.1fdB",slider8), slider8!=0);
    opacity_button.draw_button(gfx_x-pad3,row2y, sprintf(#,"O: %u%%",slider6*100), slider6>=0.01);
    c8.draw_colorbutton(gfx_x-pad3, row2y, "15+16", !!(slider7&128));
    c7.draw_colorbutton(gfx_x-pad3, row2y, "13+14", !!(slider7&64));
    c6.draw_colorbutton(gfx_x-pad3, row2y, "11+12", !!(slider7&32));
    c5.draw_colorbutton(gfx_x-pad3, row2y, "9+10", !!(slider7&16));
    c4.draw_colorbutton(gfx_x-pad3, row2y, "7+8", !!(slider7&8));
    c3.draw_colorbutton(gfx_x-pad3, row2y, "5+6", !!(slider7&4));
    c2.draw_colorbutton(gfx_x-pad3, row2y, "3+4", !!(slider7&2));
    c1.draw_colorbutton(gfx_x-pad3, row2y, "1+2", !!(slider7&1));
  );

  gfx_a=1; gfx_dest=-1;
);


fftidx != (slider1|0) ? (
  fftidx=slider1|0;
  fftsize=2^(min(max(fftidx,0),11)+4);
  integr_reset_all();
);

lslope != slider8 || windowsize != fftsize ? (
  lslope=slider8;
  i=0;
  loop(fftsize*0.5,
    frq=i/fftsize*srate*0.5;
    ampl=freq2note(frq)*slider8/12;
    slope_coeffs[i]=ampl*log(10)/20*2;
    i+=1;
  );
  old_w=0;
);

windowsize != fftsize || windowtype != (slider4|0) ? (
  windowtype=slider4|0;
  windowsize=fftsize;
  dwindowpos = $pi*2/fftsize;
  i=pwr=0;
  loop(fftsize*.5+1,
     windowpos=i*dwindowpos;
     pwr += (window[i] = (
       windowtype==1 ? 0.53836 - cos(windowpos)*0.46164 :
       windowtype==2 ? 0.35875 - 0.48829 * cos(windowpos) + 0.14128 * cos(2*windowpos) - 0.01168 * cos(3*windowpos) :
       windowtype==3 ? 0.42 - 0.50 * cos(windowpos) + 0.08 * cos(2.0*windowpos) :
        1.0));
     i+=1;
  );
  pwr=.5/(pwr*2-window[i-1]);
  loop(fftsize*.5+1,window[i-=1]*=pwr);
  integr_reset_all()
);

gfx_x=gfx_y=0; gfx_blit(0,1,0);
c8.draw_spectrum();
c7.draw_spectrum();
c6.draw_spectrum();
c5.draw_spectrum();
c4.draw_spectrum();
c3.draw_spectrum();
c2.draw_spectrum();
c1.draw_spectrum();
show_ctrls ? (
  gfx_x=gfx_y=0;
  gfx_blit(1,1,0);
);

// cursor cross + readout
!show_ctrls && mouse_x>0 && mouse_x<gfx_w && mouse_y>0 && mouse_y<gfx_h && !cap_drag ? (
  gfx_set(0,.5,1);
  gfx_line(0,mouse_y,gfx_w,mouse_y,1);
  gfx_line(mouse_x,0,mouse_x,gfx_h,1);
  read_freq=((exp(mouse_x/wsc))-1)/800*srate;
  read_n=round(freq2note(read_freq));
  strcpy(read_nn=#,"");
  read_n>=0 && read_n<=127 ? strcpy(read_nn,note2notename(read_n,1));
  read.draw_readout(mouse_x, mouse_y, slider8==0 ? (
    read_amp=20*log10(exp((20-mouse_y)/sc));
    sprintf(#,"%.2fHz %s\n\n%.2fdB",read_freq,read_nn,read_amp);
  ) : sprintf(#,"%.2fHz %s",read_freq,read_nn));
);

